plugins {
    id("java")
    id("application")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("com.diffplug.spotless") version "6.25.0"
}

group = "ua.beengoo"
version = "1.1.0"

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    dependencies {
        compileOnly(libs.libLombok)
        annotationProcessor(libs.libLombok)
        testCompileOnly(libs.libLombok)
        testAnnotationProcessor(libs.libLombok)

        implementation(libs.libSpringBootStarter)
        implementation(libs.libSpringBootStarterDataJpa)
        implementation(libs.libPostgresQL)

        testImplementation(libs.libJunitJupiter)
        testImplementation(libs.libMockito)
        testImplementation(libs.libMockitoJupiter)
    }

    spotless {
        java {
            target('src/**/*.java')
            googleJavaFormat('1.17.0').reflowLongStrings()
            removeUnusedImports()
            endWithNewline()
            trimTrailingWhitespace()
            indentWithSpaces(4)
        }
        format('misc') {
            target('*.gradle', 'gradle/**/*.gradle', '**/.gitignore', '**/*.md')
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}
